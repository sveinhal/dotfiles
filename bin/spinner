#!/usr/bin/env zsh
set -uo pipefail

readonly DIM='\033[2m'
readonly RESET='\033[0m'
readonly CLEAR_LINE='\033[2K'

typeset -A styles
styles=(
    dots    'â ‹ â ™ â ¹ â ¸ â ¼ â ´ â ¦ â § â ‡ â '
    star    'Â· âœ¢ âœ¶ âœ³ âœ» âœ³ âœ¶ âœ¢'
    moon    'ğŸŒ‘ ğŸŒ’ ğŸŒ“ ğŸŒ” ğŸŒ• ğŸŒ– ğŸŒ— ğŸŒ˜'
    clock   'ğŸ• ğŸ•‘ ğŸ•’ ğŸ•“ ğŸ•” ğŸ•• ğŸ•– ğŸ•— ğŸ•˜ ğŸ•™ ğŸ•š ğŸ•›'
    arrow   'â† â†– â†‘ â†— â†’ â†˜ â†“ â†™'
    box     'â–– â–˜ â– â–—'
)

style=dots
while getopts "s:" opt; do
    case $opt in
        s) style=$OPTARG ;;
    esac
done
shift $((OPTIND - 1))

if [[ $# -eq 0 ]]; then
    echo "Usage: spinner [-s style] <command> [args...]" >&2
    echo "Styles: ${(kj:, :)styles}" >&2
    exit 1
fi

if [[ -z "${styles[$style]:-}" ]]; then
    echo "Unknown style: $style" >&2
    echo "Available: ${(kj:, :)styles}" >&2
    exit 1
fi

readonly SPINNER_CHARS=( ${=styles[$style]} )

fifo=$(mktemp -u)
mkfifo "$fifo"

cleanup() {
    exec 3<&- 2>/dev/null
    printf "${CLEAR_LINE}\r" >&2
    rm -f "$fifo"
}
trap cleanup EXIT
trap 'kill "$cmd_pid" 2>/dev/null; exit 130' INT TERM

"$@" >"$fifo" 2>&1 &
cmd_pid=$!
exec 3<"$fifo"

spinner_idx=1

while true; do
    # read -t 0.08 doubles as both "check for output" and spinner delay
    if IFS= read -r -t 0.08 -u 3 line; then
        printf "${CLEAR_LINE}\r%s\n" "$line" >&2
        # Drain any immediately available lines
        while IFS= read -r -t 0 -u 3 line; do
            printf "%s\n" "$line" >&2
        done
    elif ! kill -0 "$cmd_pid" 2>/dev/null; then
        # Command finished â€” drain remaining output
        while IFS= read -r -u 3 line; do
            printf "${CLEAR_LINE}\r%s\n" "$line" >&2
        done
        break
    fi

    printf "${CLEAR_LINE}\r${DIM}${SPINNER_CHARS[$spinner_idx]} %s${RESET}" "$*" >&2
    spinner_idx=$(( (spinner_idx % ${#SPINNER_CHARS[@]}) + 1 ))
done

printf "${CLEAR_LINE}\r" >&2
exec 3<&-

wait "$cmd_pid"
exit $?
